module

public import Metrology.ProbLang.CtxStep
import Metrology.ProbLang.Syntax.Notation
public import Mathlib.Order.Defs.PartialOrder

@[expose] public section

noncomputable section
open Classical MeasureTheory ProbabilityTheory Measure

namespace ProbLang


variable {rT : Type _} [LawfulProbLangℝ rT]

def nsteps (r : α → α → Prop) : ℕ → α → α → Prop
  | 0,   a, b => a = b
  | n+1, a, b => ∃ c, r a c ∧ nsteps r n c b

/-- Pushforward of a `dirac` along a measurable map. Measurability-based version of
`Measure.map_dirac`, avoiding the `MeasurableSingletonClass` hypothesis (which we do
not have for general `rT`). -/
theorem map_dirac' {α β : Type _} [MeasurableSpace α] [MeasurableSpace β]
    {f : α → β} (hf : Measurable f) (a : α) : (dirac a).map f = dirac (f a) := by
  ext s hs
  rw [Measure.map_apply hf hs, Measure.dirac_apply' _ (hf hs), Measure.dirac_apply' _ hs]
  rfl

structure PureStep (e1 e2 : Exp rT) : Prop where
  safe : ∀ σ, Reducible e1 σ
  det  : ∀ σ, primStep ⟨e1, σ⟩ = dirac ⟨e2, σ⟩

class PureExec (φ : outParam Prop) (n : outParam ℕ) (e1 e2 : Exp rT) : Prop where
  pure_exec : φ → nsteps PureStep n e1 e2

structure PureHeadStep (e1 e2 : Exp rT) : Prop where
  safe : ∀ σ : State rT, HeadReducible e1 σ
  det  : ∀ σ : State rT, headStep ⟨e1, σ⟩ = dirac ⟨e2, σ⟩
  dec  : e1.decompItem = none

theorem PureHeadStep.toPureStep {e1 e2 : Exp rT} (h : PureHeadStep e1 e2) :
   PureStep e1 e2 :=
  ⟨fun σ => by rw [Reducible, primStep_eq_headStep h.dec]; exact h.safe σ,
   fun σ => primStep_eq_headStep h.dec ▸ h.det σ⟩

theorem PureStep.fill (K : Ectx rT) {e1 e2 : Exp rT} (h : PureStep e1 e2) :
    PureStep (K.fill e1) (K.fill e2) := by
  refine ⟨fun σ => (h.safe σ).fill K, fun σ => ?_⟩
  have hm : Measurable (fun ρ : Cfg rT => (⟨K.fill ρ.expr, ρ.state⟩ : Cfg rT)) := by measurability
  rw [primStep_fill (val_stuck (h.safe σ)), h.det σ]
  exact map_dirac' hm _

theorem PureStep.fill_nsteps (K : Ectx rT) {n : ℕ} {e1 e2 : Exp rT}
    (h : nsteps PureStep n e1 e2) :
    nsteps PureStep n (K.fill e1) (K.fill e2) := by
  induction n generalizing e1 e2 with
  | zero => simp [nsteps] at h; subst h; simp [nsteps]
  | succ n ih =>
    obtain ⟨c, hstep, hrest⟩ := h
    exact ⟨K.fill c, hstep.fill K, ih hrest⟩

theorem PureExec.fill (K : Ectx rT) {φ : Prop} {n : ℕ} {e1 e2 : Exp rT}
    [h : PureExec φ n e1 e2] : PureExec φ n (K.fill e1) (K.fill e2) where
  pure_exec hφ := PureStep.fill_nsteps K (h.pure_exec hφ)

theorem PureExec.reducible {σ : State rT} {φ : Prop} {n : ℕ} {e1 e2 : Exp rT}
    (hφ : φ) [h : PureExec φ (n + 1) e1 e2] :
    Reducible e1 σ := by
  obtain ⟨_, hstep, _⟩ := h.pure_exec hφ
  exact hstep.safe σ

theorem PureExec.not_val {φ : Prop} {n : ℕ} {e1 e2 : Exp rT}
    (hφ : φ) [h : PureExec φ (n + 1) e1 e2] :
    ¬e1.isValue := by
  obtain ⟨_, hstep, _⟩ := h.pure_exec hφ
  exact val_stuck (hstep.safe default)

theorem rtc_pure_step_val {n : ℕ} {v : Val rT} {e : Exp rT}
    (h : nsteps PureStep n v.1 e) :
    e.toVal? = some v := by
  induction n generalizing e with
  | zero =>
    simp [nsteps] at h
    subst h
    exact Exp.toVal?_ofVal v
  | succ n ih =>
    obtain ⟨c, hstep, hrest⟩ := h
    exact absurd v.2.toIsValue (val_stuck (hstep.safe default))

-- PureHeadStep.of_det
/-- Build a `PureHeadStep e1 e2` from a proof that `headStep` always maps `⟨e1, σ⟩`
to `dirac ⟨e2, σ⟩`. The `safe` field is derived automatically. -/
theorem PureHeadStep.of_det (e1 e2 : Exp rT)
    (hdec : e1.decompItem = none)
    (hdet : ∀ σ, headStep ⟨e1, σ⟩ = dirac ⟨e2, σ⟩) :
    PureHeadStep e1 e2 := by
  refine ⟨fun σ => ?_, hdet, hdec⟩
  unfold HeadReducible; rw [hdet σ]; simp

/-- A single deterministic head step at a fixed state `σ`. -/
structure DetHeadStep (cfg1 cfg2 : Cfg rT) : Prop where
  safe : HeadReducible cfg1.expr cfg1.state
  det  : headStep cfg1 = dirac cfg2
  dec  : cfg1.expr.decompItem = none

theorem DetHeadStep.pos {cfg1 cfg2 : Cfg rT} (h : DetHeadStep cfg1 cfg2) :
    0 < headStep cfg1 {cfg2} := by rw [h.det]; simp

theorem DetHeadStep.of_det (cfg1 cfg2 : Cfg rT)
    (hdec : cfg1.expr.decompItem = none)
    (hdet : headStep cfg1 = dirac cfg2) : DetHeadStep cfg1 cfg2 where
  safe := by obtain ⟨e1, σ1⟩ := cfg1; unfold HeadReducible; rw [hdet]; simp
  det  := hdet
  dec  := hdec

-- DetStep

structure DetStep (cfg1 cfg2 : Cfg rT) : Prop where
  safe : Reducible cfg1.expr cfg1.state
  det  : primStep cfg1 = dirac cfg2

theorem DetStep.pos {cfg1 cfg2 : Cfg rT} (h : DetStep cfg1 cfg2) :
    0 < primStep cfg1 {cfg2} := by rw [h.det]; simp

theorem DetHeadStep.toDetStep {cfg1 cfg2 : Cfg rT} (h : DetHeadStep cfg1 cfg2) :
    DetStep cfg1 cfg2 where
  safe := by obtain ⟨e1, σ1⟩ := cfg1; rw [Reducible, primStep_eq_headStep h.dec]; exact h.safe
  det := by obtain ⟨e1, σ1⟩ := cfg1; rw [primStep_eq_headStep h.dec]; exact h.det

class DetExec (n : ℕ) (cfg1 cfg2 : Cfg rT) : Prop where
  det_exec : nsteps DetStep n cfg1 cfg2

theorem DetExec.succ {cfg1 cfg2 cfg3 : Cfg rT} {n : ℕ}
    (hstep : DetStep cfg1 cfg2) [hrest : DetExec n cfg2 cfg3] :
    DetExec (n + 1) cfg1 cfg3 where
  det_exec := ⟨cfg2, hstep, hrest.det_exec⟩

theorem DetHeadStep.fst_pair {e1 e2 : Exp rT} (h1 : IsVal e1) (h2 : IsVal e2) (σ : State rT) :
    DetHeadStep ⟨.fst (.pair e1 e2), σ⟩ ⟨e1, σ⟩ :=
  .of_det _ _ (by obtain ⟨_, hd⟩ := Exp.toVal?_eq_some_of_isValue (⟨.pair h1 h2⟩ : (Exp.pair e1 e2).isValue); simp [Exp.decompItem, hd]) (by simp [headStep, Exp.isValM_some' h1, Exp.isValM_some' h2])

theorem DetHeadStep.snd_pair {e1 e2 : Exp rT} (h1 : IsVal e1) (h2 : IsVal e2) (σ : State rT) :
    DetHeadStep ⟨.snd (.pair e1 e2), σ⟩ ⟨e2, σ⟩ :=
  .of_det _ _ (by obtain ⟨_, hd⟩ := Exp.toVal?_eq_some_of_isValue (⟨.pair h1 h2⟩ : (Exp.pair e1 e2).isValue); simp [Exp.decompItem, hd]) (by simp [headStep, Exp.isValM_some' h1, Exp.isValM_some' h2])

theorem DetHeadStep.cond_true (et ef : Exp rT) (σ : State rT) :
    DetHeadStep ⟨.cond pl(#(.bool true)) et ef, σ⟩ ⟨et, σ⟩ :=
  .of_det _ _ (by obtain ⟨_, hd⟩ := Exp.toVal?_eq_some_of_isValue (e := (Exp.lit (.bool true) : Exp rT)) ⟨.lit⟩; simp [Exp.decompItem, hd]) (by simp [headStep])

theorem DetHeadStep.cond_false (et ef : Exp rT) (σ : State rT) :
    DetHeadStep ⟨.cond pl(#(.bool false)) et ef, σ⟩ ⟨ef, σ⟩ :=
  .of_det _ _ (by obtain ⟨_, hd⟩ := Exp.toVal?_eq_some_of_isValue (e := (Exp.lit (.bool false) : Exp rT)) ⟨.lit⟩; simp [Exp.decompItem, hd]) (by simp [headStep])

theorem DetHeadStep.app_lam {body v : Exp rT}
    (hlam : (Exp.lam body).IsLocallyClosed) (hv : IsVal v) (σ : State rT) :
    DetHeadStep ⟨.app (.lam body) v, σ⟩ ⟨Exp.open' body v, σ⟩ :=
  .of_det _ _ (by
    obtain ⟨_, ha⟩ := Exp.toVal?_eq_some_of_isValue (⟨hv⟩ : v.isValue)
    obtain ⟨_, hb⟩ := Exp.toVal?_eq_some_of_isValue (⟨.lam hlam⟩ : (Exp.lam body).isValue)
    simp [Exp.decompItem, ha, hb]) (by simp [headStep, Exp.isValM_some' hv])

/-- `PureHeadStep` for `(λ. body) v` when `v` is a value and the lambda is closed. -/
theorem PureHeadStep.app_lam {body v : Exp rT}
    (hlam : (Exp.lam body).IsLocallyClosed) (hv : IsVal v) :
    PureHeadStep (.app (.lam body) v) (Exp.open' body v) :=
  .of_det _ _ (by
    obtain ⟨_, h1⟩ := Exp.toVal?_eq_some_of_isValue (⟨hv⟩ : v.isValue)
    obtain ⟨_, h2⟩ := Exp.toVal?_eq_some_of_isValue (⟨.lam hlam⟩ : (Exp.lam body).isValue)
    simp [Exp.decompItem, h1, h2]) fun σ => by simp [headStep, Exp.isValM_some' hv]

/-- `PureExec` instance: `(λ. body) v` beta-reduces in 1 step when `v` is a value
and the lambda is locally closed. -/
instance pureExec_app_lam {body v : Exp rT} :
    PureExec (v.isValue ∧ (Exp.lam body).IsLocallyClosed) 1
      (.app (.lam body) v) (Exp.open' body v) where
  pure_exec h := ⟨_, (PureHeadStep.app_lam h.2 h.1.some).toPureStep, rfl⟩

/-- `PureHeadStep` for `if true then et else ef → et`. -/
theorem PureHeadStep.cond_true (et ef : Exp rT) :
    PureHeadStep (.cond pl(#(.bool true)) et ef) et :=
  .of_det _ _ (by obtain ⟨_, h⟩ := Exp.toVal?_eq_some_of_isValue (e := (Exp.lit (.bool true) : Exp rT)) ⟨.lit⟩; simp [Exp.decompItem, h]) fun σ => by simp [headStep]

/-- `PureHeadStep` for `if false then et else ef → ef`. -/
theorem PureHeadStep.cond_false (et ef : Exp rT) :
    PureHeadStep (.cond pl(#(.bool false)) et ef) ef :=
  .of_det _ _ (by obtain ⟨_, h⟩ := Exp.toVal?_eq_some_of_isValue (e := (Exp.lit (.bool false) : Exp rT)) ⟨.lit⟩; simp [Exp.decompItem, h]) fun σ => by simp [headStep]

instance pureExec_cond_true {et ef : Exp rT} :
    PureExec True 1 (.cond pl(#(.bool true)) et ef) et where
  pure_exec _ := ⟨_, (PureHeadStep.cond_true et ef).toPureStep, rfl⟩

instance pureExec_cond_false {et ef : Exp rT} :
    PureExec True 1 (.cond pl(#(.bool false)) et ef) ef where
  pure_exec _ := ⟨_, (PureHeadStep.cond_false et ef).toPureStep, rfl⟩

/-- `PureHeadStep` for `fst (v1, v2) → v1` when both are values. -/
theorem PureHeadStep.fst_pair {e1 e2 : Exp rT} (h1 : IsVal e1) (h2 : IsVal e2) :
    PureHeadStep (.fst (.pair e1 e2)) e1 :=
  .of_det _ _ (by obtain ⟨_, h⟩ := Exp.toVal?_eq_some_of_isValue (⟨.pair h1 h2⟩ : (Exp.pair e1 e2).isValue); simp [Exp.decompItem, h]) fun σ => by simp [headStep, Exp.isValM_some' h1, Exp.isValM_some' h2]

/-- `PureHeadStep` for `snd (v1, v2) → v2`. -/
theorem PureHeadStep.snd_pair {e1 e2 : Exp rT} (h1 : IsVal e1) (h2 : IsVal e2) :
    PureHeadStep (.snd (.pair e1 e2)) e2 :=
  .of_det _ _ (by obtain ⟨_, h⟩ := Exp.toVal?_eq_some_of_isValue (⟨.pair h1 h2⟩ : (Exp.pair e1 e2).isValue); simp [Exp.decompItem, h]) fun σ => by simp [headStep, Exp.isValM_some' h1, Exp.isValM_some' h2]

instance pureExec_fst_pair {e1 e2 : Exp rT} :
    PureExec (e1.isValue ∧ e2.isValue) 1 (.fst (.pair e1 e2)) e1 where
  pure_exec h := ⟨_, (PureHeadStep.fst_pair h.1.some h.2.some).toPureStep, rfl⟩

instance pureExec_snd_pair {e1 e2 : Exp rT} :
    PureExec (e1.isValue ∧ e2.isValue) 1 (.snd (.pair e1 e2)) e2 where
  pure_exec h := ⟨_, (PureHeadStep.snd_pair h.1.some h.2.some).toPureStep, rfl⟩

/-- `PureHeadStep` for `case (inl v) el er → el v`. -/
theorem PureHeadStep.case_inl {v el er : Exp rT} (hv : IsVal v) :
    PureHeadStep (.case (.inl v) el er) (el.app v) :=
  .of_det _ _ (by obtain ⟨_, h⟩ := Exp.toVal?_eq_some_of_isValue (⟨.inl hv⟩ : (Exp.inl v).isValue); simp [Exp.decompItem, h]) fun σ => by simp [headStep, Exp.isValM_some' hv]

/-- `PureHeadStep` for `case (inr v) el er → er v`. -/
theorem PureHeadStep.case_inr {v el er : Exp rT} (hv : IsVal v) :
    PureHeadStep (.case (.inr v) el er) (er.app v) :=
  .of_det _ _ (by obtain ⟨_, h⟩ := Exp.toVal?_eq_some_of_isValue (⟨.inr hv⟩ : (Exp.inr v).isValue); simp [Exp.decompItem, h]) fun σ => by simp [headStep, Exp.isValM_some' hv]

instance pureExec_case_inl {v el er : Exp rT} :
    PureExec v.isValue 1 (.case (.inl v) el er) (el.app v) where
  pure_exec hv := ⟨_, (PureHeadStep.case_inl hv.some).toPureStep, rfl⟩

instance pureExec_case_inr {v el er : Exp rT} :
    PureExec v.isValue 1 (.case (.inr v) el er) (er.app v) where
  pure_exec hv := ⟨_, (PureHeadStep.case_inr hv.some).toPureStep, rfl⟩

/-- `PureHeadStep` for `binop op v1 v2 → r` when both are values and eval succeeds. -/
theorem PureHeadStep.binop {op : BinOp} {e1 e2 r : Exp rT}
    (h1 : IsVal e1) (h2 : IsVal e2) (heval : op.eval e1 e2 = some r) :
    PureHeadStep (.binop op e1 e2) r :=
  .of_det _ _ (by obtain ⟨_, hb1⟩ := Exp.toVal?_eq_some_of_isValue (⟨h1⟩ : e1.isValue); obtain ⟨_, hb2⟩ := Exp.toVal?_eq_some_of_isValue (⟨h2⟩ : e2.isValue); simp [Exp.decompItem, hb1, hb2]) fun σ => by
    simp [headStep, Option.unwrapM, Exp.isValM_some' h1, Exp.isValM_some' h2, heval]

instance pureExec_binop {op : BinOp} {e1 e2 r : Exp rT} :
    PureExec (e1.isValue ∧ e2.isValue ∧ op.eval e1 e2 = some r) 1
      (.binop op e1 e2) r where
  pure_exec h := ⟨_, (PureHeadStep.binop h.1.some h.2.1.some h.2.2).toPureStep, rfl⟩

/-- `PureHeadStep` for `unop op v → r`. -/
theorem PureHeadStep.unop {op : UnOp} {e r : Exp rT}
    (hv : IsVal e) (heval : op.eval e = some r) :
    PureHeadStep (.unop op e) r :=
  .of_det _ _ (by obtain ⟨_, h⟩ := Exp.toVal?_eq_some_of_isValue (⟨hv⟩ : e.isValue); simp [Exp.decompItem, h]) fun σ => by
    simp [headStep, Option.unwrapM, Exp.isValM_some' hv, heval]

instance pureExec_unop {op : UnOp} {e r : Exp rT} :
    PureExec (e.isValue ∧ op.eval e = some r) 1 (.unop op e) r where
  pure_exec h := ⟨_, (PureHeadStep.unop h.1.some h.2).toPureStep, rfl⟩

-- PureHeadStep.app_fix
/-- `PureHeadStep` for `(fix body) v → (open' body (fix body)) v` when `v` is a value
and the fixpoint is closed. -/
theorem PureHeadStep.app_fix {body v : Exp rT}
    (hfix : (Exp.fix body).IsLocallyClosed) (hv : IsVal v) :
    PureHeadStep (.app (.fix body) v) (Exp.app (Exp.open' body (.fix body)) v) :=
  .of_det _ _ (by
    obtain ⟨_, h1⟩ := Exp.toVal?_eq_some_of_isValue (⟨hv⟩ : v.isValue)
    obtain ⟨_, h2⟩ := Exp.toVal?_eq_some_of_isValue (⟨.fix hfix⟩ : (Exp.fix body).isValue)
    simp [Exp.decompItem, h1, h2]) fun σ => by simp [headStep, Exp.isValM_some' hv]

instance pureExec_app_fix {body v : Exp rT} :
    PureExec (v.isValue ∧ (Exp.fix body).IsLocallyClosed) 1
      (.app (.fix body) v) (Exp.app (Exp.open' body (.fix body)) v) where
  pure_exec h := ⟨_, (PureHeadStep.app_fix h.2 h.1.some).toPureStep, rfl⟩

/-- `PureHeadStep` for `scrut v p` when match succeeds. -/
theorem PureHeadStep.scrut_some {v : Exp rT} {p : Pat rT} {b : Exp rT}
    (hv : IsVal v) (hmatch : Pat.tryMatch p v = some b) :
    PureHeadStep (.scrut v p) (.inl b) :=
  .of_det _ _ (by obtain ⟨_, h⟩ := Exp.toVal?_eq_some_of_isValue (⟨hv⟩ : v.isValue); simp [Exp.decompItem, h]) fun σ => by simp [headStep, Exp.isValM_some' hv, hmatch]

/-- `PureHeadStep` for `scrut v p` when match fails. -/
theorem PureHeadStep.scrut_none {v : Exp rT} {p : Pat rT}
    (hv : IsVal v) (hmatch : Pat.tryMatch p v = none) :
    PureHeadStep (.scrut v p) (.inr pl(#(.unit))) :=
  .of_det _ _ (by obtain ⟨_, h⟩ := Exp.toVal?_eq_some_of_isValue (⟨hv⟩ : v.isValue); simp [Exp.decompItem, h]) fun σ => by simp [headStep, Exp.isValM_some' hv, hmatch]

instance pureExec_scrut_some {v : Exp rT} {p : Pat rT} {b : Exp rT} :
    PureExec (v.isValue ∧ Pat.tryMatch p v = some b) 1 (.scrut v p) (.inl b) where
  pure_exec h := ⟨_, (PureHeadStep.scrut_some h.1.some h.2).toPureStep, rfl⟩

instance pureExec_scrut_none {v : Exp rT} {p : Pat rT} :
    PureExec (v.isValue ∧ Pat.tryMatch p v = none) 1 (.scrut v p) (.inr pl(#(.unit))) where
  pure_exec h := ⟨_, (PureHeadStep.scrut_none h.1.some h.2).toPureStep, rfl⟩

theorem DetHeadStep.app_fix {body v : Exp rT}
    (hfix : (Exp.fix body).IsLocallyClosed) (hv : IsVal v) (σ : State rT) :
    DetHeadStep ⟨.app (.fix body) v, σ⟩
      ⟨Exp.app (Exp.open' body (.fix body)) v, σ⟩ :=
  .of_det _ _ (by
    obtain ⟨_, ha⟩ := Exp.toVal?_eq_some_of_isValue (⟨hv⟩ : v.isValue)
    obtain ⟨_, hb⟩ := Exp.toVal?_eq_some_of_isValue (⟨.fix hfix⟩ : (Exp.fix body).isValue)
    simp [Exp.decompItem, ha, hb]) (by simp [headStep, Exp.isValM_some' hv])

theorem DetHeadStep.unop {op : UnOp} {e result : Exp rT}
    (hv : IsVal e)
    (heval : UnOp.eval op e = some result) (σ : State rT) :
    DetHeadStep ⟨.unop op e, σ⟩ ⟨result, σ⟩ :=
  .of_det _ _ (by obtain ⟨_, hd⟩ := Exp.toVal?_eq_some_of_isValue (⟨hv⟩ : e.isValue); simp [Exp.decompItem, hd]) (by simp [headStep, Option.unwrapM, Exp.isValM_some' hv, heval])

theorem DetHeadStep.binop {op : BinOp} {e1 e2 result : Exp rT}
    (h1 : IsVal e1) (h2 : IsVal e2)
    (heval : BinOp.eval op e1 e2 = some result) (σ : State rT) :
    DetHeadStep ⟨.binop op e1 e2, σ⟩ ⟨result, σ⟩ :=
  .of_det _ _ (by obtain ⟨_, hd1⟩ := Exp.toVal?_eq_some_of_isValue (⟨h1⟩ : e1.isValue); obtain ⟨_, hd2⟩ := Exp.toVal?_eq_some_of_isValue (⟨h2⟩ : e2.isValue); simp [Exp.decompItem, hd1, hd2]) (by simp [headStep, Option.unwrapM, Exp.isValM_some' h1, Exp.isValM_some' h2, heval])

theorem DetHeadStep.case_inl {v el er : Exp rT} (hv : IsVal v) (σ : State rT) :
    DetHeadStep ⟨.case (.inl v) el er, σ⟩ ⟨el.app v, σ⟩ :=
  .of_det _ _ (by obtain ⟨_, hd⟩ := Exp.toVal?_eq_some_of_isValue (⟨.inl hv⟩ : (Exp.inl v).isValue); simp [Exp.decompItem, hd]) (by simp [headStep, Exp.isValM_some' hv])

theorem DetHeadStep.case_inr {v el er : Exp rT} (hv : IsVal v) (σ : State rT) :
    DetHeadStep ⟨.case (.inr v) el er, σ⟩ ⟨er.app v, σ⟩ :=
  .of_det _ _ (by obtain ⟨_, hd⟩ := Exp.toVal?_eq_some_of_isValue (⟨.inr hv⟩ : (Exp.inr v).isValue); simp [Exp.decompItem, hd]) (by simp [headStep, Exp.isValM_some' hv])

theorem DetHeadStep.alloc {v : Exp rT} (hv : IsVal v) (σ : State rT) :
    DetHeadStep ⟨.alloc v, σ⟩ ⟨pl(#(.loc σ.heap.fresh)), σ.update_heap (·.insert σ.heap.fresh ⟨v, hv, hv.lc⟩)⟩ := by
  obtain ⟨w, hw⟩ := hv.check?_some
  exact .of_det _ _ (by obtain ⟨_, hd⟩ := Exp.toVal?_eq_some_of_isValue (⟨hv⟩ : v.isValue); simp [Exp.decompItem, hd]) (by simp [headStep, Exp.asValM, Exp.toVal?, hw, IsVal.subsingleton hv w])

theorem DetHeadStep.load {ℓ : Loc} {v : Val rT} (σ : State rT) (hlookup : σ.heap[ℓ]? = some v) :
    DetHeadStep ⟨pl(!#(.loc ℓ)), σ⟩ ⟨.ofVal v, σ⟩ :=
  .of_det _ _ (by obtain ⟨_, hd⟩ := Exp.toVal?_eq_some_of_isValue (e := (Exp.lit (.loc ℓ) : Exp rT)) ⟨.lit⟩; simp [Exp.decompItem, hd]) (by simp [headStep, hlookup])

theorem DetHeadStep.store {ℓ : Loc} {e : Exp rT} {v_old v_new : Val rT}
    (_hv : IsVal e) (σ : State rT)
    (hlookup : σ.heap[ℓ]? = some v_old)
    (hnew : e.toVal? = some v_new) :
    DetHeadStep ⟨.store pl(#(.loc ℓ)) e, σ⟩ ⟨pl(#(.unit)), σ.update_heap (·.insert ℓ v_new)⟩ :=
  .of_det _ _ (by obtain ⟨_, hd⟩ := Exp.toVal?_eq_some_of_isValue (e := (Exp.lit (.loc ℓ) : Exp rT)) ⟨.lit⟩; simp [Exp.decompItem, hnew, hd]) (by simp [headStep, Exp.asValM, hnew, hlookup])

theorem DetStep.fill (K : Ectx rT) {cfg1 cfg2 : Cfg rT} (h : DetStep cfg1 cfg2) :
    DetStep ⟨K.fill cfg1.expr, cfg1.state⟩ ⟨K.fill cfg2.expr, cfg2.state⟩ where
  safe := h.safe.fill K
  det := by
    obtain ⟨e1, σ1⟩ := cfg1
    have hm : Measurable (fun ρ : Cfg rT => (⟨K.fill ρ.expr, ρ.state⟩ : Cfg rT)) := by measurability
    rw [primStep_fill (val_stuck h.safe), h.det]
    exact map_dirac' hm _

theorem DetExec.refl (cfg : Cfg rT) : DetExec 0 cfg cfg where
  det_exec := rfl

theorem DetExec.cons {cfg1 cfg2 cfg3 : Cfg rT} {n : ℕ}
    (hstep : DetStep cfg1 cfg2) (hrest : DetExec n cfg2 cfg3) :
    DetExec (n + 1) cfg1 cfg3 where
  det_exec := ⟨cfg2, hstep, hrest.det_exec⟩

end ProbLang
end
