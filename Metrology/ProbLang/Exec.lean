module

public import Metrology.ProbLang.Measure
public import Metrology.ProbLang.HeadStep
public import Metrology.ProbLang.DetStep
public import Metrology.ProbLang.Discrete
public import Metrology.Couplings.AdditiveCouplings

@[expose] public section

noncomputable section
open Classical MeasureTheory ProbabilityTheory Measure ProbLang

namespace ProbLang


variable {rT : Type _} [LawfulProbLangℝ rT]

def execN (n : Nat) (ρ : Cfg rT) : Measure (Cfg rT) :=
  match n with
  | 0 => 0
  | n + 1 => if ρ.expr.isValue then dirac ρ else (primStep ρ).bind (execN n)

@[fun_prop]
theorem execN_measurable (n : Nat) : Measurable (execN (rT := rT) n) := by
  induction n
  · simp [execN]
  · exact Measurable.ite (by measurability) (by measurability) (by measurability)

/-- execN conditioned on terminating in exactly N steps -/
def execExactN (N : Nat) (ρ : Cfg rT) : Measure (Cfg rT) :=
  match N with
  | 0 => if ρ.expr.isValue then dirac ρ else 0
  | N + 1 => if ρ.expr.isValue then 0 else (primStep ρ).bind (execExactN N)

@[fun_prop]
theorem execExactN_measurable (n : Nat) : Measurable (execExactN (rT := rT) n) := by
  induction n
  · simp only [execExactN]
    exact Measurable.ite (by measurability) (by measurability) (by measurability)
  · simp only [execExactN]
    exact Measurable.ite (by measurability) (by measurability) (by measurability)

theorem execExactN_sum_continuous {n : Nat} {ρ : Cfg rT} {S} (HS : MeasurableSet S) :
    execN n ρ S = ∑'(N : Nat), if N < n then execExactN N ρ S else 0 := by
  induction n generalizing ρ with
  | zero => simp [execN]
  | succ n ih =>
    simp only [execN]
    by_cases hv : ρ.expr.isValue
    · simp only [↓reduceIte, hv]
      rw [tsum_eq_zero_add' ENNReal.summable]
      simp only [Nat.zero_lt_succ, ↓reduceIte, execExactN, hv]
      simp
    · simp only [↓reduceIte, hv]
      rw [tsum_eq_zero_add' ENNReal.summable]
      have Hzero : (if 0 < n + 1 then (execExactN 0 ρ) S else 0) = 0 := by simp [execExactN, hv]
      rw [Hzero, zero_add]; clear Hzero
      rw [bind_apply HS (by measurability)]
      simp_rw [ih]
      rw [lintegral_tsum (fun k => ?G3)]
      case G3 =>
        refine Measurable.aemeasurable ?_
        refine Measurable.ite (by measurability) ?_ (by measurability)
        exact (measurable_coe HS).comp (execExactN_measurable _)
      congr 1; ext k
      by_cases hk : k < n
      · have hk' : ∀ k, k + 1 < n + 1 ↔ k < n  := by omega
        simp only [hk, hk', ↑reduceIte]
        rw [← bind_apply HS ?G5]
        case G5 =>
          refine Measurable.aemeasurable ?_
          measurability
        simp only [execExactN, hv, ↑reduceIte]
      · simp [hk]

theorem execExactN_mono_continuous {n : Nat} {ρ : Cfg rT} {S} (HS : MeasurableSet S) :
    execExactN n ρ S ≤ execN (n + 1) ρ S := by
  have Hunfold : execExactN n ρ S = (if n < n + 1 then execExactN n ρ S else 0) := by simp
  rw [execExactN_sum_continuous HS, Hunfold]
  exact ENNReal.le_tsum n

-- DISCRETE: `Discrete.tsum_dirac_mul [Countable rT] [MeasurableSingletonClass rT]`
--   `(ρ : Cfg rT) (f : Cfg rT → ENNReal) : ∑' ρ', dirac ρ {ρ'} * f ρ' = f ρ`

-- Probably not going to generalize this unless I need it for SampCert

/-- Limiting distribution of an execution, over configurations -/
def limExec (ρ : Cfg rT) : Measure (Cfg rT) := ⨆ (i : ℕ), execN i ρ

/-- Extract an expression measure from a Cfg measure -/
def asExpr (μ : Measure (Cfg rT)) : Measure (Exp rT) := μ.map (·.expr)

/-- Limiting distribution of an execution, over return values -/
def limExecV (ρ : Cfg rT) : Measure (Exp rT) := asExpr <| limExec ρ

/-! ### Measurability for arbitrary measurable `rT`. -/

-- TODO: Move me
theorem asExpr.measurable :
    Measurable (asExpr : Measure (Cfg rT) → Measure (Exp rT)) :=
  Measure.measurable_map _ Cfg.measurable_expr

theorem ENNReal.tsum_iSup_of_monotone_cts {f : ℕ → Cfg rT → ENNReal}
    (hf : ∀ a, Monotone (f · a)) (hm : ∀ x, Measurable (f x)) :
    ∑' a, ⨆ n, f n a = ⨆ n, ∑' a, f n a := by
  simp_rw [← MeasureTheory.lintegral_count]
  exact MeasureTheory.lintegral_iSup (fun _ => hm _) (fun _ _ hmn a => hf a hmn)

theorem iSup_measure_apply {f : ℕ → Measure (Cfg rT)} (hf : Monotone f)
    {S : Set (Cfg rT)} (HS : MeasurableSet S) :
    (⨆ i, f i) S = ⨆ i, f i S :=
  Measure.iSup_apply_of_monotone f hf HS

theorem Measure.bind_mono_right' {α β : Type*} [MeasurableSpace α] [MeasurableSpace β]
    (μ : Measure α) (f g : α → Measure β) (Hf : Measurable f) (Hg : Measurable g)
    (h : ∀ a, f a ≤ g a) :
    μ.bind f ≤ μ.bind g := by
  refine le_intro fun S HS HNE => ?_
  rw [bind_apply HS Hf.aemeasurable, bind_apply HS Hg.aemeasurable]
  exact lintegral_mono (fun a => h a S)

theorem execN_succ_le' (n : ℕ) (ρ : Cfg rT) : execN n ρ ≤ execN (n + 1) ρ := by
  induction n generalizing ρ with
  | zero => exact bot_le
  | succ k ih =>
    rw [execN]
    rw [execN]
    split
    · exact le_refl _
    · apply Measure.bind_mono_right'
      · measurability
      · measurability
      · exact ih

/-! ### Primitive unfoldings -/

@[simp] theorem execN_zero (ρ : Cfg rT) : execN 0 ρ = 0 := rfl

@[simp] theorem execN_succ_isValue {ρ : Cfg rT} (hv : ρ.expr.isValue) (n : Nat) :
    execN (n + 1) ρ = dirac ρ := by
  simp [execN, hv]

theorem execN_succ_not_isValue {ρ : Cfg rT} (hv : ¬ ρ.expr.isValue) (n : Nat) :
    execN (n + 1) ρ = (primStep ρ).bind (execN n) := by
  simp [execN, hv]

def stepOrFinal (ρ : Cfg rT) : Measure (Cfg rT) :=
  if ρ.expr.isValue then dirac ρ else primStep ρ

theorem stepOrFinal_isValue {ρ : Cfg rT} (hv : ρ.expr.isValue) :
    stepOrFinal ρ = dirac ρ := by
  simp [stepOrFinal, hv]

theorem stepOrFinal_not_isValue {ρ : Cfg rT} (hv : ¬ ρ.expr.isValue) :
    stepOrFinal ρ = primStep ρ := by
  simp [stepOrFinal, hv]

@[fun_prop]
theorem stepOrFinal.measurable :
    Measurable (stepOrFinal : Cfg rT → Measure (Cfg rT)) := by
  have hpred : MeasurableSet {ρ : Cfg rT | ρ.expr.isValue} := by
    -- `isValue` now also requires local closedness; `{isValue} = {isValueR} ∩ {lcb 0 = true}`.
    have : {ρ : Cfg rT | ρ.expr.isValue}
        = (fun ρ : Cfg rT => ρ.expr) ⁻¹' ({e | e.isValueR} ∩ {e | Exp.lcb 0 e = true}) := by
      ext ρ; simp [Exp.isValue_iff_isValueR, Set.mem_inter_iff]
    rw [this]
    exact Cfg.measurable_expr ((Exp.isValueR.measurable.setOf).inter Exp.lcb_zero.measurableSet)
  exact Measurable.ite hpred measurable_dirac primStep.measurable

/-! ### Monotonicity (ported from `SampCert/SLang.lean`) -/

theorem execN_mono : ∀ {n m : ℕ} (_ : n ≤ m) (ρ : Cfg rT), execN n ρ ≤ execN m ρ := by
  intro n m h ρ
  induction h with
  | refl => exact le_refl _
  | step h ih => exact le_trans ih (execN_succ_le' _ ρ)

theorem execN_monotone : Monotone fun i ↦ execN (rT := rT) i ρ := fun _ _ h => execN_mono h _

theorem execN_mono_singleton [MeasurableSingletonClass rT]
    {n m : ℕ} (h : n ≤ m) (ρ : Cfg rT) (c : Cfg rT) :
    execN n ρ {c} ≤ execN m ρ {c} :=
  execN_mono h ρ {c}

@[fun_prop]
theorem limExec.measurable : Measurable (limExec : Cfg rT → Measure (Cfg rT)) :=
  Measure.measurable_iSup_countable (fun n => execN_measurable n)
    (fun ρ _ _ h => execN_mono h ρ)

@[fun_prop]
theorem limExecV.measurable : Measurable (limExecV : Cfg rT → Measure (Exp rT)) :=
  asExpr.measurable.comp limExec.measurable


/-! ### Sub-probability -/

theorem execN_univ_le_one
    (n : Nat) (ρ : Cfg rT) : (execN n ρ) Set.univ ≤ 1 := by
  induction n generalizing ρ with
  | zero => simp [execN]
  | succ k ih =>
    unfold execN
    by_cases hv : ρ.expr.isValue
    · simp [hv]
    · simp only [hv, ↓reduceIte]
      rw [bind_apply (by measurability) (Measurable.aemeasurable (by measurability))]
      calc ∫⁻ a, (execN k a) Set.univ ∂(primStep ρ)
          ≤ ∫⁻ _, 1 ∂(primStep ρ) := lintegral_mono fun a => ih a
        _ = (primStep ρ) Set.univ := by simp
        _ ≤ 1 := primStep_univ_le_one ρ

/-! ### Algebraic laws

We do **not** port Rocq's `exec_plus` / `stepN_plus` directly: our `execN`
collapses Rocq's `pexec` (whose `iterM 0 = dret`) and `exec` (whose
`exec 0 non-final = dzero`) into a single function with `execN 0 = 0`.
Because of the 0-fuel boundary, the clean Rocq identity
`exec (n + m) = pexec n ≫= exec m` has no clean analogue in our port.
Instead, we work with `stepOrFinal` iterates when we need factoring, and
with `execN` directly otherwise. -/

/-! ### `limExec` basics (ported from `SampCert/SLang.lean`) -/

-- Rocq: lim_exec_final (value case)
theorem limExec_of_isVal {e : Exp rT} {σ : State rT} (Hv : IsVal e) :
    limExec ⟨e, σ⟩ = dirac ⟨e, σ⟩ := by
  unfold limExec
  have hv : e.isValue := ⟨Hv⟩
  apply le_antisymm
  · apply iSup_le; intro n; cases n with
    | zero => exact bot_le
    | succ n => simp [execN, hv]
  · exact le_iSup_of_le 1 (by simp [execN, hv])

theorem limExec_not_final -- [Countable rT] [MeasurableSingletonClass rT]
    {e : Exp rT} {σ : State rT} (Hnv : ¬ e.isValue) :
    limExec ⟨e, σ⟩ = (primStep ⟨e, σ⟩).bind limExec := by
  have hmono : Monotone (fun n => execN n ⟨e, σ⟩) := fun _ _ h => execN_mono h _
  have hstep : Monotone fun n => (primStep ⟨e, σ⟩).bind (execN n) := fun n m h =>
    Measure.bind_mono_right' _ _ _ (execN_measurable n) (execN_measurable m) fun a => execN_mono h a
  rw [limExec, ← hmono.iSup_nat_add 1]
  simp_rw [execN_succ_not_isValue (ρ := ⟨e, σ⟩) Hnv]
  refine Measure.ext fun S HS => ?_
  rw [iSup_measure_apply hstep HS, Measure.bind_apply HS limExec.measurable.aemeasurable]
  calc ⨆ n, ((primStep ⟨e, σ⟩).bind (execN n)) S
      = ⨆ n, ∫⁻ a, execN n a S ∂primStep ⟨e, σ⟩ :=
        iSup_congr fun n => Measure.bind_apply HS (execN_measurable n).aemeasurable
    _ = ∫⁻ a, ⨆ n, execN n a S ∂primStep ⟨e, σ⟩ :=
        (lintegral_iSup (fun n => (measurable_coe HS).comp (execN_measurable n))
          fun n m h a => execN_mono h a S).symm
    _ = ∫⁻ a, limExec a S ∂primStep ⟨e, σ⟩ :=
        lintegral_congr fun a => (iSup_measure_apply (fun _ _ h => execN_mono h a) HS).symm

theorem limExec_step
    (ρ : Cfg rT) :
    limExec ρ = (if ρ.expr.isValue then dirac ρ else primStep ρ).bind limExec := by
  obtain ⟨e, σ⟩ := ρ
  by_cases hv : e.isValue
  · simp only [hv, ↑reduceIte]
    rw [Measure.dirac_bind (by measurability)]
  · simp only [hv, ↑reduceIte]
    exact limExec_not_final hv

/-- `limExec_step` written in terms of `stepOrFinal`. -/
theorem limExec_step'  (ρ : Cfg rT) : limExec ρ = (stepOrFinal ρ).bind limExec := by
  rw [limExec_step]; rfl

/-! ### `pexecN` — iterated `stepOrFinal` -/
def pexecN (n : Nat) (ρ : Cfg rT) : Measure (Cfg rT) :=
  match n with
  | 0 => dirac ρ
  | n + 1 => (stepOrFinal ρ).bind (pexecN n)

@[fun_prop]
theorem pexecN_measurable {n : Nat} : Measurable (pexecN (rT := rT) n) := by
  induction n
  · simp [pexecN]
    measurability
  · simp [pexecN]
    measurability

@[simp] theorem pexecN_zero (ρ : Cfg rT) : pexecN 0 ρ = dirac ρ := rfl

theorem pexecN_succ (n : Nat) (ρ : Cfg rT) :
    pexecN (n + 1) ρ = (stepOrFinal ρ).bind (pexecN n) := rfl

theorem pexecN_one (ρ : Cfg rT) : pexecN 1 ρ = stepOrFinal ρ := by
  show (stepOrFinal ρ).bind (pexecN 0) = stepOrFinal ρ
  show (stepOrFinal ρ).bind dirac = stepOrFinal ρ
  exact Measure.bind_dirac

theorem pexecN_plus (n m : Nat) (ρ : Cfg rT) : pexecN (n + m) ρ = (pexecN n ρ).bind (pexecN m) := by
  induction n generalizing ρ with
  | zero =>
    simp
    rw [Measure.dirac_bind (by measurability)]
  | succ k ih =>
    rw [show (k + 1 + m) = (k + m) + 1 by linarith]
    simp [pexecN]
    rw [MeasureTheory.Measure.bind_bind]
    · congr 1
      grind
    · refine Measurable.aemeasurable ?_
      measurability
    · refine Measurable.aemeasurable ?_
      measurability

theorem pexecN_det_trans {n m : Nat} {ρ ρ' ρ'' : Cfg rT} (Hn : pexecN n ρ = dirac ρ')
    (Hm : pexecN m ρ' = dirac ρ'') : pexecN (n + m) ρ = dirac ρ'' := by
  rw [pexecN_plus, Hn, dirac_bind pexecN_measurable, Hm]

theorem limExec_pexecN (n : Nat) (ρ : Cfg rT) : limExec ρ = (pexecN n ρ).bind limExec := by
  induction n generalizing ρ with
  | zero =>
    rw [pexecN_zero, Measure.dirac_bind]
    measurability
  | succ k ih =>
    rw [pexecN_succ]
    conv_lhs => rw [limExec_step']
    rw [Measure.bind_bind]
    · congr 1
      funext ρ'
      exact ih ρ'
    · refine Measurable.aemeasurable ?_
      measurability
    · refine Measurable.aemeasurable ?_
      measurability

/-! ### `limExec` application and mass -/

theorem limExec_apply (ρ : Cfg rT) (HS : MeasurableSet S) :
    limExec ρ S = ⨆ n, (execN n ρ) S :=
  iSup_measure_apply execN_monotone  HS

theorem limExec_univ' (ρ : Cfg rT) : (limExec ρ) .univ = ⨆ n, (execN n ρ) .univ :=
  limExec_apply _ .univ

/-! ### Pointwise and mass bounds -/

theorem limExec_leq_setwise {ρ : Cfg rT} {S : Set (Cfg rT)} {r : ENNReal} (HS : MeasurableSet S)
    (H : ∀ n, (execN n ρ) S ≤ r) : (limExec ρ) S ≤ r := by
  rw [limExec_apply _ HS]
  exact iSup_le H

-- Rocq: lim_exec_leq
theorem limExec_leq_mass  {ρ : Cfg rT} {r : ENNReal}
    (H : ∀ n, (execN n ρ) Set.univ ≤ r) : (limExec ρ) Set.univ ≤ r := by
  rw [limExec_univ']; exact iSup_le H

theorem limExec_term  {ρ : Cfg rT} {n : Nat} (Hv : (execN n ρ) Set.univ = 1) :
    limExec ρ = execN n ρ := by
  have hfin_n : IsFiniteMeasure (execN n ρ) :=
    ⟨by rw [Hv]; exact ENNReal.one_lt_top⟩
  have hk_eq : ∀ k, n ≤ k → execN k ρ = execN n ρ := by
    intro k hk
    have hk_univ : (execN k ρ) Set.univ = 1 := by
      refine le_antisymm (execN_univ_le_one k ρ) ?_
      calc (1 : ENNReal) = (execN n ρ) Set.univ := Hv.symm
        _ ≤ (execN k ρ) Set.univ := (execN_mono hk ρ) _
    exact (Measure.eq_of_le_of_measure_univ_eq (execN_mono hk ρ) (Hv.trans hk_univ.symm)).symm
  ext
  rename_i S HS
  rw [limExec_apply _ HS]
  apply le_antisymm
  · apply iSup_le; intro k
    by_cases hkn : k ≤ n
    · have X := execN_mono hkn ρ
      exact measure_mono_both (execN_mono hkn ρ) (fun ⦃a⦄ => id)
    · rw [hk_eq k (Nat.le_of_not_le hkn)]
  · exact le_iSup_of_le n (le_refl _)

/-! ### Deterministic trace -/

-- Ah... this one is this way because long-running executions becomes zeroed out
theorem limExec_det_final {ρ ρ' : Cfg rT} {n : Nat} (H : (execN n ρ) = dirac ρ') :
    limExec ρ = dirac ρ' := by
  have htot : (execN n ρ) Set.univ = 1 := H ▸ dirac_apply_of_mem trivial
  rw [limExec_term htot]
  ext
  rename_i S _
  exact DFunLike.congr_fun H S

/-! ### lintegral against limExec -/

-- Good exercise
theorem lintegral_limExec'
    (ρ : Cfg rT) (f : Cfg rT → ENNReal) :
    ∫⁻ x, f x ∂(limExec ρ) = ⨆ n, ∫⁻ x, f x ∂(execN n ρ) := by
  unfold limExec
  apply le_antisymm
  · rw [lintegral_def]
    refine iSup_le fun g => iSup_le fun hg => ?_
    have hstep : g.lintegral (⨆ i, execN i ρ) = ⨆ n, g.lintegral (execN n ρ) := by
      have hms : ∀ x, MeasurableSet (⇑g ⁻¹' {x}) := fun x => g.measurableSet_preimage _
      simp_rw [MeasureTheory.SimpleFunc.lintegral,
        show ∀ x, (⨆ i, execN i ρ) (⇑g ⁻¹' {x}) = ⨆ n, (execN n ρ) (⇑g ⁻¹' {x})
          from fun x => iSup_measure_apply execN_monotone (hms x),
        ENNReal.mul_iSup]
      exact ENNReal.finsetSum_iSup_of_monotone
        (fun x _ _ h => mul_le_mul' le_rfl ((execN_mono h ρ) _))
    rw [hstep]
    refine iSup_mono fun n => ?_
    rw [lintegral_def]
    exact le_iSup_of_le g (le_iSup_of_le hg le_rfl)
  · exact iSup_le fun n => lintegral_mono' (le_iSup (fun i => execN i ρ) n) le_rfl

/-! ### Additive coupling lift (Approxis glue) -/

-- Rocq: lim_exec_ARcoupl, specialized to additive form.
-- If every finite unrolling is AddCoupl-related to μ₂ at slack ε, so is limExec.
theorem limExec_AddCoupl
    {β : Type*} [MeasurableSpace β] {ε : ENNReal}
    {Φ : Set (Cfg rT × β)} {ρ : Cfg rT} {μ₂ : Measure β}
    (H : ∀ n, AddCoupl ε Φ (execN n ρ) μ₂) :
    AddCoupl ε Φ (limExec ρ) μ₂ := by
  intro ⟨f, hf, hfb⟩ ⟨g, hg, hgb⟩ hfg
  rw [lintegral_limExec' ρ f]
  refine iSup_le fun n => ?_
  exact H n ⟨f, hf, hfb⟩ ⟨g, hg, hgb⟩ hfg

/-- Additive-coupling lift through a *pushforward* of `limExec`.

`limExec` is the increasing supremum of the finite unrollings, and
`lintegral_limExec'` turns the left lintegral against it into the supremum of the
left lintegrals against the unrollings — no atoms and no countability. Pushing
forward along a measurable `F` commutes with that step (`lintegral_map`), so an
`AddCoupl` that holds for every `(execN n ρ).map F` holds for `(limExec ρ).map F`.

This is the countability-free replacement for the
`AddCoupl.map_inv` → `limExec_AddCoupl` → `AddCoupl.map` round trip: `map_inv`
needs `F`'s codomain to carry a discrete σ-algebra, and here it is only used to
undo a pushforward that `AddCoupl.iSup_left` can carry along directly. -/
theorem limExec_map_AddCoupl
    {γ β : Type*} [MeasurableSpace γ] [MeasurableSpace β] {ε : ENNReal}
    {Φ : Set (γ × β)} {ρ : Cfg rT} {μ₂ : Measure β}
    {F : Cfg rT → γ} (hF : Measurable F)
    (H : ∀ n, AddCoupl ε Φ ((execN n ρ).map F) μ₂) :
    AddCoupl ε Φ ((limExec ρ).map F) μ₂ := by
  refine AddCoupl.iSup_left (fun f hf => ?_) H
  simp_rw [lintegral_map hf hF]
  exact le_of_eq (lintegral_limExec' ρ (fun a => f (F a)))

/-- `limExec_map_AddCoupl` at `F := Cfg.expr`: the form `adequacy` needs. -/
theorem limExecV_AddCoupl
    {β : Type*} [MeasurableSpace β] {ε : ENNReal}
    {Φ : Set (Exp rT × β)} {ρ : Cfg rT} {μ₂ : Measure β}
    (H : ∀ n, AddCoupl ε Φ (asExpr (execN n ρ)) μ₂) :
    AddCoupl ε Φ (limExecV ρ) μ₂ :=
  limExec_map_AddCoupl Cfg.measurable_expr H

end ProbLang
end
