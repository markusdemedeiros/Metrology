module

public import Mathlib.Data.ENNReal.Basic
public import Iris
public import Iris.Algebra.View
public import Iris.Instances.IProp.Instance
public import Iris.Algebra.Auth
public import Iris.Algebra.Numbers
public import Metrology.Iris.Algebra

@[expose] public section

noncomputable section

open Std Iris COFE
open scoped ENNReal NNReal

abbrev ErrorCredit : Type _ := ℝ≥0∞

instance : COFE ErrorCredit := COFE.ofDiscrete _ Eq_Equivalence
instance : OFE.Discrete ErrorCredit := ⟨id⟩
instance (x : ErrorCredit) : OFE.DiscreteE x := ⟨OFE.Discrete.discrete_0⟩

instance : CMRA ErrorCredit where
  pcore _ := some 0
  op := (· + ·)
  ValidN _ ε := ε < 1
  Valid ε := ε < 1
  op_ne.ne _ _ _ h := by rw [h]
  pcore_ne _ := by rintro ⟨rfl⟩; exists 0
  validN_ne {_ _ _} := by rintro ⟨rfl⟩; exact id
  valid_iff_validN := .symm <| forall_const Nat
  validN_succ := (·)
  validN_op_left {n x y} H := lt_of_add_lt_of_nonneg_left H (zero_le)
  assoc {_ _ _} := (add_assoc ..).symm
  comm {_ _} := (add_comm ..).symm
  pcore_op_left {_ _} := by rintro ⟨rfl⟩; simp [OFE.Equiv]
  pcore_idem := by simp
  pcore_op_mono {_ _} := by rintro ⟨rfl⟩ _; exists 0; simp
  extend _ h := ⟨_, _, OFE.discrete h, .rfl, .rfl⟩

instance : CMRA.Discrete ErrorCredit where
  discrete_valid := id

instance : UCMRA ErrorCredit where
  unit := 0
  unit_valid := by simp [CMRA.Valid]
  unit_left_id := by simp [CMRA.op]
  pcore_unit := by simp [CMRA.pcore]

theorem ErrorCredit.included_iff {ε₁ ε₂ : ErrorCredit} : ε₁ ≼ ε₂ ↔ ε₁ ≤ ε₂ := by
  refine ⟨?_, (⟨ε₂ - ε₁, add_tsub_cancel_of_le · |>.symm⟩)⟩
  rintro ⟨ε₃, rfl⟩
  exact le_self_add

theorem ErrorCredit.includedN_iff {ε₁ ε₂ : ErrorCredit} : ε₁ ≼{n} ε₂ ↔ ε₁ ≤ ε₂ :=
  ErrorCredit.included_iff

instance {ε : ErrorCredit} : CMRA.Cancelable ε where
  cancelableN {n ε₁ ε₂} := by
    simp [CMRA.ValidN, CMRA.op, OFE.Dist]
    intro H1 H2
    refine (ENNReal.add_right_inj ?_).mp H2
    rintro rfl; simp at H1

theorem ErrorCredit.localUpdate {ε₁ ε₂ ε₁' ε₂' : ErrorCredit} (h1 : ε₂' <= ε₂)
    (h2 : ε₁ + ε₂' = ε₁' + ε₂) : (ε₁, ε₂) ~l~> (ε₁', ε₂') := by
  rintro n (_|ε) <;> simp only [OFE.Dist, CMRA.op?, CMRA.ValidN, CMRA.op]
  · rintro H rfl
    symm at h2
    obtain rfl : ε₁' = ε₂' := ENNReal.add_left_inj H.ne_top |>.mp (by grind)
    exact ⟨Std.lt_of_le_of_lt h1 H, rfl⟩
  · rintro H rfl
    have Hnt : ε₂ ≠ ∞ := by rintro rfl; simp at H
    obtain rfl : (ε + ε₂') = ε₁' :=
      ENNReal.add_left_inj Hnt |>.mp (.trans (add_rotate ..).symm h2)
    refine ⟨?_, by grind⟩
    refine Std.lt_of_le_of_lt ?_ H
    rw [add_comm]
    exact (add_le_add_left h1 ε)

instance : Iris.IsUnit (◯ 0 : Auth ErrorCredit) where
  unit_valid := Auth.frag_valid.mpr (by simp [CMRA.Valid])
  unit_left_id := by simp [CMRA.op]
  pcore_unit := .rfl

class ECPreGS (GF : BundledGFunctors) where
  ec : ElemG GF (constOF (Auth ErrorCredit))

attribute [reducible, instance] ECPreGS.ec

class ECGS (GF : BundledGFunctors) extends ECPreGS GF where
  γec : GName

section Resources

variable {GF : BundledGFunctors} [IEC : ECGS GF]

def ecAuth (ε : ℝ≥0∞) : IProp GF := iOwn (E := IEC.ec) IEC.γec (● ε)
def ec (ε : ℝ≥0∞) : IProp GF := iOwn (E := IEC.ec) IEC.γec (◯ ε)
notation "↯" r:50 => ec r
notation "●↯" r:50 => ecAuth r

instance : CMRA.Discrete (Auth ErrorCredit) := by infer_instance
instance : OFE.DiscreteE (◯ r : Auth ErrorCredit) := Auth.frag_discrete (by infer_instance)

end Resources

namespace ErrorCredit

variable {GF : BundledGFunctors} [IEC : ECGS GF]

theorem ext {ε₁ ε₂} (he : ε₁ = ε₂) : ↯ε₁ ⊢@{IProp GF} ↯ε₂ := by simp [he]

theorem extAuth {ε₁ ε₂} (he : ε₁ = ε₂) : ●↯ ε₁ ⊢@{IProp GF} ●↯ ε₂ := by simp [he]

theorem split {ε₁ ε₂} : ↯(ε₁ + ε₂) ⊢@{IProp GF} ↯ε₁ ∗ ↯ε₂ := by
  unfold ec
  iintro Hε
  iapply iOwn_op
  simp [CMRA.op]

theorem difference {ε₁ ε₂} (Hwf : ε₁ ≤ ε₂) : ↯ε₂ ⊢@{IProp GF} ↯ε₁ ∗ ↯(ε₂ - ε₁) := by
  iintro H
  iapply split
  iapply ext (add_tsub_cancel_of_le Hwf).symm $$ H

theorem combine {ε₁ ε₂} : ↯ε₁ ∗ ↯ε₂ ⊢@{IProp GF} ↯(ε₁ + ε₂) := by
  unfold ec
  iintro H
  ihave _ := iOwn_op (E := IEC.ec) |>.mpr $$ H
  simp [CMRA.op]

theorem zero : ⊢@{IProp GF} |==> ↯0 := iOwn_unit

theorem supply_bound {εₛ ε} : ⊢@{IProp GF} ●↯ εₛ -∗ ↯ε -∗ ⌜ε ≤ εₛ⌝ := by
  unfold ec ecAuth
  iintro Hs Hε
  ihave Hv := iOwn_cmraValid_op (E := IEC.ec) $$ [Hs Hε]
  · isplitl [Hs] <;> first | iexact Hs | iexact Hε
  ihave %hv := internalCmraValid_discrete (A := Auth ErrorCredit) (PROP := IProp GF) $$ Hv
  ipureintro
  obtain ⟨hinc, _⟩ := Auth.auth_both_valid.mp hv
  exact ErrorCredit.includedN_iff.mp (hinc 0)

theorem supply_decrease {εₛ ε} : ⊢@{IProp GF} ●↯ εₛ -∗ ↯ε -∗ |==> ●↯ (εₛ - ε) := by
  iintro Hs Hε
  ihave %Hle := supply_bound (GF := GF) $$ Hs Hε
  unfold ec ecAuth
  ihave Hc := iOwn_op (E := IEC.ec) |>.mpr $$ [Hs Hε]
  · isplitl [Hs] <;> first | iexact Hs | iexact Hε
  refine iOwn_update <| Auth.auth_update_dealloc ?_
  simp only [UCMRA.unit]
  refine localUpdate (zero_le) ?_
  simpa [add_zero] using (tsub_add_cancel_of_le Hle).symm

theorem supply_increase {ε₁ ε₂ : ℝ≥0∞} (h : ε₁ + ε₂ < 1) :
    ●↯ ε₁ ⊢@{IProp GF} |==> (●↯ (ε₁ + ε₂) ∗ ↯ε₂) := by
  unfold ec ecAuth
  have Hupd : (● ε₁) ~~> (● ε₁ + ε₂) • (◯ ε₂ : Auth ℝ≥0∞) := by
    refine Auth.auth_update_alloc <| (local_update_unital_discrete ..).mpr ?_
    simp only [CMRA.Valid, OFE.Equiv, CMRA.op, UCMRA.unit, zero_add, forall_apply_eq_imp_iff]
    exact fun _ => ⟨h, add_comm _ _⟩
  iintro Hε
  -- FIXME: Is this fixed by the last update to master?
  -- imod (iOwn_update Hupd) with H'
  -- Application type mismatch: The argument
  --   Hupd
  -- has type
  --   (● ε₁) ~~> (● ε₁ + ε₂) • ◯ ε₂
  -- but is expected to have type
  --   ?m.102 ~~> ?m.103
  -- in the application
  --   iOwn_update Hupd
  suffices Hup :
      iOwn (E := IEC.ec) (ECGS.γec GF) (● ε₁)
      ⊢ |==> iOwn (E := IEC.ec) (ECGS.γec GF) ((● ε₁ + ε₂) • (◯ ε₂)) by
    refine .trans Hup ?_
    iintro H
    imod H
    imodintro
    iapply iOwn_op
    iexact H
  refine iOwn_update Hupd

theorem weaken {ε₁ ε₂ : ℝ≥0∞} (h : ε₂ ≤ ε₁) : ↯ε₁ ⊢@{IProp GF} ↯ε₂ := by
  iintro Hε
  have hsplit : ε₁ = (ε₁ - ε₂) + ε₂ := (tsub_add_cancel_of_le h).symm
  rw [hsplit]
  ihave ⟨_, H⟩ := split (GF := GF) $$ Hε
  iexact H

theorem valid {ε : ℝ≥0∞} : ↯ε ⊢@{IProp GF} ⌜ε < 1⌝ := by
  unfold ec
  iintro Hε
  ihave Hv := iOwn_cmraValid (E := IEC.ec) $$ Hε
  ihave %hv := internalCmraValid_discrete (A := Auth ErrorCredit) (PROP := IProp GF) $$ Hv
  ipureintro
  exact Auth.frag_valid.mp hv

theorem contradict {ε : ℝ≥0∞} (h : 1 ≤ ε) : ↯ε ⊢@{IProp GF} False := by
  iintro Hε
  ihave %hle := valid (GF := GF) $$ Hε
  exact absurd h (Std.not_le.mpr hle)

namespace Induction

theorem err_amp_power {ε : ℝ≥0∞} {k : ℝ≥0} (hε : 0 < ε) (hk : 1 < k) :
    ∃ n : ℕ, 1 ≤ ε * (k : ℝ≥0∞) ^ n := by
  rcases eq_or_ne ε ∞ with (rfl | hε')
  · exact ⟨0, by simp⟩
  · lift ε to ℝ≥0 using hε' with ε
    obtain ⟨n, hn⟩ := pow_unbounded_of_one_lt ε⁻¹ (by exact_mod_cast hk)
    have hlift : (1 : ℝ≥0) ≤ ε * k ^ n := by
      rw [mul_comm]
      refine (inv_le_iff_one_le_mul₀ ?_).mp hn.le
      exact_mod_cast hε
    exact ⟨n, by exact_mod_cast hlift⟩

theorem err_amp_mult {ε ε' : ℝ≥0∞} {k : ℝ≥0} (hε : 0 < ε) (hle : ε ≤ ε') (hk : 1 < k) :
    ∃ n : ℕ, 1 ≤ (n : ℝ≥0∞) * ((k : ℝ≥0∞) - 1) * ε + ε' := by
  rcases eq_or_ne ε ∞ with rfl | hεne
  · exact ⟨0, by simp [top_le_iff.mp hle]⟩
  · lift ε to ℝ≥0 using hεne with ε
    obtain ⟨n, hn⟩ := exists_nat_ge ((k - 1) * ε)⁻¹
    refine ⟨n, le_add_right ?_⟩
    have h1 : (1 : ℝ≥0) ≤ n * (k - 1) * ε := by
      rw [mul_assoc]
      refine (inv_le_iff_one_le_mul₀ ?_).mp hn
      exact mul_pos (tsub_pos_of_lt hk) (by exact_mod_cast hε)
    have hcoe : ((k : ℝ≥0∞) - 1) = ((k - 1 : ℝ≥0) : ℝ≥0∞) := by push_cast; rfl
    rw [hcoe]
    exact_mod_cast h1

theorem simple {ε : ℝ≥0∞} {k : ℝ≥0} {P : IProp GF} (hε : 0 < ε) (hk : 1 < k) :
    □ ((↯(k * ε) -∗ P) ∗ ↯ε -∗ P) ⊢@{IProp GF} ↯ε -∗ P := by
  suffices haux : ∀ ε', ε ≤ ε' → □ ((↯(k * ε) -∗ P) ∗ ↯ε -∗ P) ⊢@{IProp GF} ↯ε' -∗ P by
    iapply haux _ le_rfl
  iintro %ε' %Hhle #Hamp Hε
  obtain ⟨n, Hn⟩ := err_amp_mult hε Hhle hk
  induction n generalizing ε'
  next =>
    iexfalso; simp at Hn; iapply contradict Hn $$ Hε
  next n IH =>
    have hk1 : (1 : ℝ≥0∞) ≤ (k : ℝ≥0∞) := by exact_mod_cast hk.le
    have Hkε : (k : ℝ≥0∞) * ε = ((k : ℝ≥0∞) - 1) * ε + ε := by
      conv_lhs => rw [← tsub_add_cancel_of_le hk1, add_mul, one_mul]
    have Hεeq : ε' = (ε' - ε) + ε := (tsub_add_cancel_of_le Hhle).symm
    set ε'' : ℝ≥0∞ := ε' + ((k : ℝ≥0∞) - 1) * ε
    have Hε''eq : (ε' - ε) + (k : ℝ≥0∞) * ε = ε'' := by
      rw [Hkε, ← add_assoc, add_right_comm, ← Hεeq]
    have Hn' : 1 ≤ (n : ℝ≥0∞) * ((k : ℝ≥0∞) - 1) * ε + ε'' := by
      refine Hn.trans (Std.le_of_eq ?_)
      show _ = _ * _ + (ε' + _)
      rw [Nat.cast_add, Nat.cast_one, add_mul, one_mul, add_mul,
          add_assoc, add_comm (_ * ε) ε', ← add_assoc]
    rw [Hεeq]
    ihave ⟨Hε₁, Hε₂⟩ := split (GF := GF) $$ Hε
    iapply Hamp
    isplitr [Hε₂] <;> try · iexact Hε₂
    iintro Hε
    ihave Hε₃ := combine (GF := GF) $$ [Hε₁ Hε]
    · isplitl [Hε₁] <;> iassumption
    iapply IH ε'' (Hhle.trans le_self_add) Hn'
    isplitr [Hε₃]
    · iexact Hamp
    · iapply ext (GF := GF) Hε''eq $$ Hε₃

theorem external_simple {ε : ℝ≥0∞} {k : ℝ≥0} {P : IProp GF} (hε : 0 < ε) (hk : 1 < k)
    (hamp : (↯(k * ε) -∗ P) ∗ ↯ε ⊢ P) : ↯ε ⊢ P := by
  iapply simple hε hk
  iintro !> H
  iapply hamp $$ H

theorem increasing {ε : ℝ≥0∞} {ε' : ℝ≥0} {P : IProp GF} (hε : 0 < ε) (hε' : ε < ε') :
    □ ((↯ε' -∗ P) ∗ ↯ε -∗ P) ⊢@{IProp GF} ↯ε -∗ P := by
  iintro #Hamp Hε
  ihave %hε'' := valid (GF := GF) $$ Hε
  let k' : ℝ≥0∞ := ε' / ε
  have hk1 : 1 < k' := ENNReal.lt_div_iff_mul_lt (by simp) (by simp) |>.mpr (by simp [hε'])
  have Hk' : k' ≠ ∞ := ENNReal.div_ne_top ENNReal.coe_ne_top (Std.ne_of_lt hε).symm
  have Hkeq : ε' = k' * ε := by
    dsimp [k']
    have Hε : ε / ε = 1 := ENNReal.div_self (Std.ne_of_lt hε).symm (LT.lt.ne_top hε')
    simp [ENNReal.mul_comm_div, Hε]
  lift k' to ℝ≥0 using Hk' with k
  have Hk : 1 < k := ENNReal.one_lt_coe_iff.mp hk1
  iapply simple (ε := ε) (k := k) hε Hk $$ [] Hε
  imodintro
  iintro ⟨Hc, Hε⟩
  iapply Hamp
  isplitr [Hε]
  · iintro Hε
    iapply Hc
    iapply ext $$ Hε
    exact Hkeq
  · iexact Hε

theorem amplifying {ε : ℝ≥0∞} {k : ℝ≥0} {P : IProp GF} (hε : 0 < ε) (hk : 1 < k) :
    □ (∀ {ε' : ℝ≥0∞}, ⌜0 < ε'⌝ -∗ □ (↯(k * ε') -∗ P) -∗ ↯ε' -∗ P)
    ⊢@{IProp GF} ↯ε -∗ P := by
  iintro #Hamp Hε
  obtain ⟨n, Hn⟩ := err_amp_power hε hk
  induction n generalizing ε
  next =>
    iexfalso
    simp at Hn
    iapply contradict Hn $$ Hε
  next n ih =>
    iapply Hamp $$ %_ %hε [] Hε
    imodintro
    iintro Hε
    iapply ih (ε := k * ε)
    · exact ENNReal.mul_pos_iff.mpr ⟨ENNReal.coe_pos.mpr (pos_of_gt hk), pos_of_gt hε⟩
    · refine Hn.trans ?_
      ring_nf
      exact le_rfl
    · isplitr <;> first | iexact Hamp | iexact Hε

theorem amp_external {ε : ℝ≥0∞} {k : ℝ≥0} {P : IProp GF}
    (hε : 0 < ε) (hk : 1 < k)
    (hamp : ∀ {ε'}, 0 < ε' → □ (↯((k : ℝ≥0∞) * ε') -∗ P) ∗ ↯ε' ⊢ P) :
    ↯ε ⊢ P := by
  iapply amplifying hε hk
  iintro !> %ε' %hε' #H Hε
  iapply hamp hε'
  isplitr [Hε] <;> first | iexact H | iexact Hε

end Induction

instance ec_timeless (ε : ℝ≥0∞) : BI.Timeless (↯ε : IProp GF) := iOwn_timeless

end ErrorCredit

theorem ec_alloc {GF : BundledGFunctors} [IEC : ECPreGS GF] (ε : ℝ≥0∞) (h : ε < 1) :
    ⊢@{IProp GF} |==> ∃ γ : GName,
      ecAuth (IEC := { toECPreGS := IEC, γec := γ }) ε ∗
      ec (IEC := { toECPreGS := IEC, γec := γ }) ε := by
  unfold ec ecAuth
  imod (iOwn_alloc (E := IEC.ec) ((● ε) • (◯ ε)) (Auth.auth_both_valid_2 h .rfl)) with ⟨%γ, Hγ⟩
  imodintro
  iexists γ
  iapply iOwn_op
  iexact Hγ

end
