import Mathlib.Data.Real.Basic
import Mathlib.Data.EReal.Basic
import Mathlib.MeasureTheory.Measure.MeasureSpace
import Mathlib.MeasureTheory.Constructions.BorelSpace.Basic
import Mathlib.MeasureTheory.Integral.Lebesgue.Basic
import Mathlib.MeasureTheory.Measure.Dirac
import Mathlib.MeasureTheory.Integral.Lebesgue.Countable
import Mathlib.Analysis.SpecialFunctions.Log.ERealExp
import Mathlib.MeasureTheory.Measure.GiryMonad
import Mathlib.MeasureTheory.Integral.Lebesgue.Add
import Mathlib.Topology.UnitInterval
import Mathlib.MeasureTheory.Constructions.UnitInterval
import Mathlib.Probability.ProbabilityMassFunction.Basic
import Mathlib.Probability.ProbabilityMassFunction.Constructions
import Mathlib.Analysis.Real.OfDigits

def BoundedFunction {α : Type _} (f : α → ENNReal) : Prop :=
  ∀ a, f a ≤ 1

def CouplingFunction (α : Type _) [MeasurableSpace α] :=
  { f : α → ENNReal // Measurable f ∧ BoundedFunction f}

theorem CouplingFunction.measurable {α : Type} [MeasurableSpace α] (f : CouplingFunction α) :
  Measurable f.1 := f.property.1

theorem CouplingFunction.bounded {α : Type} [MeasurableSpace α] (f : CouplingFunction α) :
    ∀ a, f.1 a ≤ 1 := f.property.2

instance {α : Type _} [MeasurableSpace α] : CoeFun (CouplingFunction α) (fun _ => α → ENNReal) where
  coe := (·.1)

section ApproximateCoupling

variable {α β : Type _} [MeasurableSpace α] [MeasurableSpace β]

open MeasureTheory

/-- Approximate relational coupling between μₗ and μᵣ on the set Φ. -/
def ARCoupling (F : ENNReal → ENNReal) (Φ : Set (α × β)) (μₗ : Measure α) (μᵣ : Measure β) :=
  ∀ (f : CouplingFunction α) (g : CouplingFunction β),
    (∀ {a b}, Φ (a, b) → f a ≤ g b) →
    ∫⁻ x, f x ∂μₗ ≤ F (∫⁻ x, g x ∂μᵣ)

namespace ARCoupling

variable {F : ENNReal → ENNReal}

open Measure

theorem refl (μ : Measure α) (HF : ∀ {x}, x ≤ F x) :
    ARCoupling F (fun v => v.1 = v.2) μ μ :=
  fun _ _ Hle => (lintegral_mono fun _ ↦ Hle rfl).trans HF

theorem dirac {a : α} {b : β} (HF : ∀ {x}, x ≤ F x) (Φ : Set (α × β)) (H : Φ (a, b)) :
    ARCoupling F Φ (.dirac a) (.dirac b) := by
  refine fun ⟨f, Hf, _⟩ ⟨g, Hg, _⟩ Hle => ?_
  refine .trans ?_ HF
  rw [lintegral_dirac' _ Hf, lintegral_dirac' _ Hg]
  exact Hle H

/-- Enlarging the output bound `F` weakens the coupling: if `F ≤ F'` pointwise then any
`ARCoupling F S` is also an `ARCoupling F' S`. -/
theorem mono_F {Φ : Set (α × β)} {μₗ : Measure α} {μᵣ : Measure β}
    (HF : ∀ x, F x ≤ F' x) (H : ARCoupling F Φ μₗ μᵣ) : ARCoupling F' Φ μₗ μᵣ :=
  fun f g Hle => (H f g Hle).trans (HF _)

/-- Enlarging the relation `S` weakens the coupling: coupling under the smaller relation
implies coupling under the larger one. -/
theorem mono_rel {Φ Φ' : Set (α × β)} {μₗ : Measure α} {μᵣ : Measure β} (HS : Φ ⊆ Φ')
    (H : ARCoupling F Φ μₗ μᵣ) : ARCoupling F Φ' μₗ μᵣ :=
  fun f g Hle => H f g fun hab => Hle (HS hab)

/-- The zero measure is trivially coupled on the left: `∫⁻ f ∂0 = 0 ≤ F _`. -/
theorem zero_left {F : ENNReal → ENNReal} (S : Set (α × β)) (μᵣ : Measure β) :
    ARCoupling F S 0 μᵣ := by intro _ _ _; simp

/-- Coupling against the zero measure on the right. -/
theorem zero_right (S : Set (α × β)) {μₗ : Measure α}
    (HF : μₗ .univ ≤ F 0) : ARCoupling F S μₗ (0 : Measure β) := by
  refine fun ⟨f, _, Hfb⟩ g Hle => ?_
  calc ∫⁻ (x : α), f x ∂μₗ
    _ ≤ μₗ .univ := lintegral_le_meas Hfb (by simp)
    _ ≤ F 0 := HF
    _ ≤ F (∫⁻ (x : β), g x ∂0) := by simp

-- TODO: Perhaps show that couplings lift when two things are measure_eq
-- Define follow lintergal_map for this proof

end ARCoupling
end ApproximateCoupling
